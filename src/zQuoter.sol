// SPDX-License-Identifier: MIT
pragma solidity ^0.8.33;

zQuoter constant ZQUOTER_BASE = zQuoter(0x658bF1A6608210FDE7310760f391AD4eC8006A5F);

contract zQuoter {
    enum AMM {
        UNI_V2,
        SUSHI,
        ZAMM,
        UNI_V3,
        UNI_V4,
        CURVE,
        LIDO,
        WETH_WRAP,
        V4_HOOKED
    }

    struct Quote {
        AMM source;
        uint256 feeBps;
        uint256 amountIn;
        uint256 amountOut;
    }

    constructor() payable {}

    function getQuotes(bool exactOut, address tokenIn, address tokenOut, uint256 swapAmount)
        public
        view
        returns (Quote memory best, Quote[] memory quotes)
    {
        return ZQUOTER_BASE.getQuotes(exactOut, tokenIn, tokenOut, swapAmount);
    }

    function quoteV2(
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        bool sushi
    ) public view returns (uint256 amountIn, uint256 amountOut) {
        return ZQUOTER_BASE.quoteV2(exactOut, tokenIn, tokenOut, swapAmount, sushi);
    }

    function quoteV3(
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint24 fee,
        uint256 swapAmount
    ) public view returns (uint256 amountIn, uint256 amountOut) {
        return ZQUOTER_BASE.quoteV3(exactOut, tokenIn, tokenOut, fee, swapAmount);
    }

    function quoteV4(
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint24 fee,
        int24 tickSpacing,
        address hooks,
        uint256 swapAmount
    ) public view returns (uint256 amountIn, uint256 amountOut) {
        return ZQUOTER_BASE.quoteV4(
            exactOut, tokenIn, tokenOut, fee, tickSpacing, hooks, swapAmount
        );
    }

    function quoteZAMM(
        bool exactOut,
        uint256 feeOrHook,
        address tokenIn,
        address tokenOut,
        uint256 idIn,
        uint256 idOut,
        uint256 swapAmount
    ) public view returns (uint256 amountIn, uint256 amountOut) {
        return ZQUOTER_BASE.quoteZAMM(
            exactOut, feeOrHook, tokenIn, tokenOut, idIn, idOut, swapAmount
        );
    }

    function limit(bool exactOut, uint256 quoted, uint256 bps) public pure returns (uint256) {
        return SlippageLib.limit(exactOut, quoted, bps);
    }

    function _asQuote(AMM source, uint256 amountIn, uint256 amountOut)
        internal
        pure
        returns (Quote memory q)
    {
        q.source = source;
        q.amountIn = amountIn;
        q.amountOut = amountOut;
    }

    /// @notice Unified single-hop quoting across all AMMs.
    function _quoteBestSingleHop(bool exactOut, address tokenIn, address tokenOut, uint256 amount)
        internal
        view
        returns (Quote memory best)
    {
        // 1. Base quoter: V2/Sushi/ZAMM/V3/V4
        (best,) = getQuotes(exactOut, tokenIn, tokenOut, amount);
        if (best.source == AMM.WETH_WRAP) best = Quote(AMM.UNI_V2, 0, 0, 0);

        // 2. Curve (skip unbuildable exactOut stable-underlying pairs)
        {
            (
                uint256 cin,
                uint256 cout,
                address pool,
                bool useUnd,
                bool isStab,
                uint8 iIdx,
                uint8 jIdx
            ) = quoteCurve(exactOut, tokenIn, tokenOut, amount, 8);
            if (pool != address(0) && !(exactOut && isStab && useUnd && iIdx > 0 && jIdx > 0)) {
                if (_isBetter(exactOut, cin, cout, best.amountIn, best.amountOut)) {
                    best = _asQuote(AMM.CURVE, cin, cout);
                }
            }
        }

        // 3. Lido
        if (tokenIn == address(0) && (tokenOut == STETH || tokenOut == WSTETH)) {
            (uint256 lin, uint256 lout) = quoteLido(exactOut, tokenOut, amount);
            if (
                (lin != 0 || lout != 0)
                    && _isBetter(exactOut, lin, lout, best.amountIn, best.amountOut)
            ) {
                best = _asQuote(AMM.LIDO, lin, lout);
            }
        }
    }

    function _v2PoolFor(address tokenA, address tokenB, bool sushi)
        internal
        pure
        returns (address v2pool, bool zeroForOne)
    {
        unchecked {
            (address token0, address token1, bool zF1) = _sortTokens(tokenA, tokenB);
            zeroForOne = zF1;
            v2pool = address(
                uint160(
                    uint256(
                        keccak256(
                            abi.encodePacked(
                                hex"ff",
                                !sushi ? V2_FACTORY : SUSHI_FACTORY,
                                keccak256(abi.encodePacked(token0, token1)),
                                !sushi ? V2_POOL_INIT_CODE_HASH : SUSHI_POOL_INIT_CODE_HASH
                            )
                        )
                    )
                )
            );
        }
    }

    // zRouter calldata builders:

    error NoRoute();

    /// @dev Normalize CURVE_ETH sentinel to address(0) so all ETH logic is consistent.
    function _normalizeETH(address token) internal pure returns (address) {
        return token == CURVE_ETH ? address(0) : token;
    }

    function _hubs() internal pure returns (address[6] memory) {
        return [WETH, USDC, USDT, DAI, WBTC, WSTETH];
    }

    function _sweepTo(address token, address to) internal pure returns (bytes memory) {
        return abi.encodeWithSelector(IRouterExt.sweep.selector, token, uint256(0), uint256(0), to);
    }

    function _isBetter(
        bool exactOut,
        uint256 newIn,
        uint256 newOut,
        uint256 bestIn,
        uint256 bestOut
    ) internal pure returns (bool) {
        return exactOut ? (newIn < bestIn || bestIn == 0) : (newOut > bestOut);
    }

    // ** CURVE

    // ====================== QUOTE (auto-discover via MetaRegistry) ======================

    // Accumulator for 2-hop hub routing
    struct HubPlan {
        bool found;
        bool isExactOut;
        address mid;
        Quote a;
        Quote b;
        bytes ca;
        bytes cb;
        uint256 scoreIn;
        uint256 scoreOut;
    }

    // Accumulator for 3-hop route discovery
    struct Route3 {
        bool found;
        Quote a;
        Quote b;
        Quote c;
        address mid1;
        address mid2;
        uint256 score;
    }

    // Accumulator to keep best candidate off the stack
    struct CurveAcc {
        uint256 bestOut;
        uint256 bestIn;
        address bestPool;
        bool usedUnderlying;
        bool usedStable;
        uint8 iIdx;
        uint8 jIdx;
    }

    // Single-hop Curve quote with deterministic discovery, returns coin indices too
    function quoteCurve(
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 maxCandidates // e.g. 8, 0 = unlimited
    )
        public
        view
        returns (
            uint256 amountIn,
            uint256 amountOut,
            address bestPool,
            bool usedUnderlying,
            bool usedStable,
            uint8 iIndex,
            uint8 jIndex
        )
    {
        if (swapAmount == 0) return (0, 0, address(0), false, true, 0, 0);

        // trivial ETH<->WETH (1:1) — let base path handle; we won't override with Curve
        if (
            (tokenIn == address(0) && tokenOut == WETH)
                || (tokenIn == WETH && tokenOut == address(0))
        ) {
            return (0, 0, address(0), false, true, 0, 0);
        }

        address a = tokenIn == address(0) ? CURVE_ETH : tokenIn;
        address b = tokenOut == address(0) ? CURVE_ETH : tokenOut;

        address[] memory pools = ICurveMetaRegistry(CURVE_METAREGISTRY).find_pools_for_coins(a, b);
        uint256 limit_ =
            (maxCandidates == 0 || maxCandidates > pools.length) ? pools.length : maxCandidates;

        CurveAcc memory acc;
        acc.bestIn = type(uint256).max;

        for (uint256 k; k < limit_; ++k) {
            address pool = pools[k];
            if (pool.code.length == 0) continue;

            (int128 i, int128 j, bool underlying) =
                ICurveMetaRegistry(CURVE_METAREGISTRY).get_coin_indices(pool, a, b, 0);

            if (i < 0 || j < 0) continue;
            if (uint256(int256(i)) > type(uint8).max) continue;
            if (uint256(int256(j)) > type(uint8).max) continue;

            (bool ok, uint256 qIn, uint256 qOut, bool isStable) =
                _curveTryQuoteOne(pool, exactOut, i, j, underlying, swapAmount);
            if (!ok) continue;

            if (exactOut) {
                if (qIn < acc.bestIn) {
                    acc.bestIn = qIn;
                    acc.bestOut = swapAmount;
                    acc.bestPool = pool;
                    acc.usedUnderlying = (underlying && isStable);
                    acc.usedStable = isStable;
                    acc.iIdx = uint8(uint256(int256(i)));
                    acc.jIdx = uint8(uint256(int256(j)));
                }
            } else {
                if (qOut > acc.bestOut) {
                    acc.bestOut = qOut;
                    acc.bestIn = swapAmount;
                    acc.bestPool = pool;
                    acc.usedUnderlying = (underlying && isStable);
                    acc.usedStable = isStable;
                    acc.iIdx = uint8(uint256(int256(i)));
                    acc.jIdx = uint8(uint256(int256(j)));
                }
            }
        }

        if (acc.bestPool == address(0)) return (0, 0, address(0), false, true, 0, 0);

        amountIn = exactOut ? acc.bestIn : swapAmount;
        amountOut = exactOut ? swapAmount : acc.bestOut;
        bestPool = acc.bestPool;
        usedUnderlying = acc.usedUnderlying;
        usedStable = acc.usedStable;
        iIndex = acc.iIdx;
        jIndex = acc.jIdx;
    }

    // Single-pool quote with ABI autodetect (stable first, else crypto).
    function _curveTryQuoteOne(
        address pool,
        bool exactOut,
        int128 i,
        int128 j,
        bool underlying,
        uint256 amt
    ) internal view returns (bool ok, uint256 amountIn, uint256 amountOut, bool usedStable) {
        // try stable (and underlying) first
        {
            bytes4 sel = exactOut
                ? (underlying
                        ? ICurveStableLike.get_dx_underlying.selector
                        : ICurveStableLike.get_dx.selector)
                : (underlying
                        ? ICurveStableLike.get_dy_underlying.selector
                        : ICurveStableLike.get_dy.selector);
            (bool s, bytes memory r) = pool.staticcall(abi.encodeWithSelector(sel, i, j, amt));
            if (s && r.length >= 32) {
                uint256 q = abi.decode(r, (uint256));
                return exactOut ? (true, q, amt, true) : (true, amt, q, true);
            }
        }

        // fallback: crypto ABI
        uint256 ui = uint256(int256(i));
        uint256 uj = uint256(int256(j));
        bytes4 sel2 = exactOut ? ICurveCryptoLike.get_dx.selector : ICurveCryptoLike.get_dy.selector;
        (bool s2, bytes memory r2) = pool.staticcall(abi.encodeWithSelector(sel2, ui, uj, amt));
        if (!s2 || r2.length < 32) return (false, 0, 0, false);
        uint256 q2 = abi.decode(r2, (uint256));
        return exactOut ? (true, q2, amt, false) : (true, amt, q2, false);
    }

    // ====================== BUILD CALLDATA (single-hop) ======================

    function _buildCurveSwapCalldata(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 slippageBps,
        uint256 deadline,
        address pool,
        bool useUnderlying,
        bool isStable,
        uint8 iIndex,
        uint8 jIndex,
        uint256 amountIn,
        uint256 amountOut
    ) internal pure returns (bytes memory callData, uint256 amountLimit, uint256 msgValue) {
        // Guard: router can't do exactOut stable-underlying when both indices are base coins
        if (exactOut && isStable && useUnderlying && iIndex > 0 && jIndex > 0) {
            return (callData, 0, 0); // empty callData signals unbuildable
        }

        uint256 st = (isStable && useUnderlying) ? 2 : 1;
        uint256 pt = isStable ? 10 : 20;
        uint256 quoted = exactOut ? amountIn : amountOut;
        amountLimit = SlippageLib.limit(exactOut, quoted, slippageBps);

        // Build calldata with assembly: avoids allocating address[11] + uint256[4][5] + address[5]
        // Layout: sel(4) + to,exactOut(64) + route[11](352) + swapParams[5][4](640)
        //       + basePools[5](160) + swapAmount,amountLimit,deadline(96) = 1316 bytes
        bytes4 sel = IZRouter.swapCurve.selector;
        callData = new bytes(1316);
        assembly ("memory-safe") {
            let p := add(callData, 32)
            mstore(p, sel)
            let s := add(p, 4)
            mstore(s, to)
            mstore(add(s, 0x20), exactOut)
            // route: [tokenIn, pool, tokenOut, 0..0]
            mstore(add(s, 0x40), tokenIn)
            mstore(add(s, 0x60), pool)
            mstore(add(s, 0x80), tokenOut)
            // swapParams[0] = [iIndex, jIndex, st, pt]  (offset 0x1a0 from s)
            mstore(add(s, 0x1a0), iIndex)
            mstore(add(s, 0x1c0), jIndex)
            mstore(add(s, 0x1e0), st)
            mstore(add(s, 0x200), pt)
            // swapAmount, amountLimit, deadline (offset 0x4c0 from s)
            mstore(add(s, 0x4c0), swapAmount)
            mstore(add(s, 0x4e0), amountLimit)
            mstore(add(s, 0x500), deadline)
        }

        msgValue = (tokenIn == address(0)) ? (exactOut ? amountLimit : swapAmount) : 0;
    }

    // ====================== LIDO QUOTE & BUILDER ======================

    /// @notice Quote ETH → stETH or ETH → wstETH via Lido staking (1:1 for stETH, rate-based for wstETH).
    function quoteLido(bool exactOut, address tokenOut, uint256 swapAmount)
        public
        view
        returns (uint256 amountIn, uint256 amountOut)
    {
        if (swapAmount == 0) return (0, 0);

        uint256 totalShares = IStETH(STETH).getTotalShares();
        uint256 totalPooled = IStETH(STETH).getTotalPooledEther();
        if (totalShares == 0 || totalPooled == 0) return (0, 0);

        if (tokenOut == STETH) {
            // ETH → stETH is 1:1
            return (swapAmount, swapAmount);
        } else if (tokenOut == WSTETH) {
            if (!exactOut) {
                // exactIn: swapAmount ETH → stETH (1:1) → wstETH
                // wstETH = stETH * totalShares / totalPooled
                uint256 wstOut = (swapAmount * totalShares) / totalPooled;
                if (wstOut == 0) return (0, 0);
                return (swapAmount, wstOut);
            } else {
                // exactOut: need swapAmount wstETH
                // ethIn = ceil(swapAmount * totalPooled / totalShares)
                uint256 ethIn = (swapAmount * totalPooled + totalShares - 1) / totalShares;
                if (ethIn == 0) return (0, 0);
                return (ethIn, swapAmount);
            }
        }

        return (0, 0);
    }

    /// @notice Build router calldata for a Lido swap (ETH → stETH or ETH → wstETH).
    function _buildLidoSwap(address to, bool exactOut, address tokenOut, uint256 swapAmount)
        internal
        pure
        returns (bytes memory callData)
    {
        if (tokenOut == STETH) {
            callData = exactOut
                ? abi.encodeWithSelector(IZRouter.ethToExactSTETH.selector, to, swapAmount)
                : abi.encodeWithSelector(IZRouter.exactETHToSTETH.selector, to);
        } else if (tokenOut == WSTETH) {
            callData = exactOut
                ? abi.encodeWithSelector(IZRouter.ethToExactWSTETH.selector, to, swapAmount)
                : abi.encodeWithSelector(IZRouter.exactETHToWSTETH.selector, to);
        } else {
            revert NoRoute();
        }
    }

    // ====================== TOP-LEVEL BUILDER (with Curve override) ======================

    function buildBestSwap(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 slippageBps,
        uint256 deadline
    )
        public
        view
        returns (Quote memory best, bytes memory callData, uint256 amountLimit, uint256 msgValue)
    {
        tokenIn = _normalizeETH(tokenIn);
        tokenOut = _normalizeETH(tokenOut);

        // ---------- ETH <-> WETH (1:1, no slippage) ----------
        if (
            (tokenIn == address(0) && tokenOut == WETH)
                || (tokenIn == WETH && tokenOut == address(0))
        ) {
            require(swapAmount != 0, ZeroAmount());
            best = _asQuote(AMM.WETH_WRAP, swapAmount, swapAmount);
            amountLimit = swapAmount; // 1:1, no slippage

            if (tokenIn == address(0)) {
                // ETH -> WETH
                msgValue = swapAmount;
                if (to == ZROUTER) {
                    callData = abi.encodeWithSelector(IRouterExt.wrap.selector, swapAmount);
                } else {
                    bytes[] memory c = new bytes[](2);
                    c[0] = abi.encodeWithSelector(IRouterExt.wrap.selector, swapAmount);
                    c[1] = abi.encodeWithSelector(
                        IRouterExt.sweep.selector, WETH, uint256(0), swapAmount, to
                    );
                    callData = abi.encodeWithSelector(IRouterExt.multicall.selector, c);
                }
            } else {
                // WETH -> ETH
                msgValue = 0;
                if (to == ZROUTER) {
                    bytes[] memory c = new bytes[](2);
                    c[0] = abi.encodeWithSelector(
                        IRouterExt.deposit.selector, WETH, uint256(0), swapAmount
                    );
                    c[1] = abi.encodeWithSelector(IRouterExt.unwrap.selector, swapAmount);
                    callData = abi.encodeWithSelector(IRouterExt.multicall.selector, c);
                } else {
                    bytes[] memory c = new bytes[](3);
                    c[0] = abi.encodeWithSelector(
                        IRouterExt.deposit.selector, WETH, uint256(0), swapAmount
                    );
                    c[1] = abi.encodeWithSelector(IRouterExt.unwrap.selector, swapAmount);
                    c[2] = abi.encodeWithSelector(
                        IRouterExt.sweep.selector, address(0), uint256(0), swapAmount, to
                    );
                    callData = abi.encodeWithSelector(IRouterExt.multicall.selector, c);
                }
            }
            return (best, callData, amountLimit, msgValue);
        }

        // ---------- Normal path ----------
        // Single unified quote across all sources (V2/Sushi/V3/V4/ZAMM/Curve/Lido)
        best = _quoteBestSingleHop(exactOut, tokenIn, tokenOut, swapAmount);
        if (best.amountIn == 0 && best.amountOut == 0) revert NoRoute();

        uint256 quoted = exactOut ? best.amountIn : best.amountOut;
        amountLimit = SlippageLib.limit(exactOut, quoted, slippageBps);

        callData = _buildCalldataFromBest(
            to, exactOut, tokenIn, tokenOut, swapAmount, amountLimit, slippageBps, deadline, best
        );

        msgValue = _requiredMsgValue(exactOut, tokenIn, swapAmount, amountLimit);
    }

    function _spacingFromBps(uint16 bps) internal pure returns (int24) {
        unchecked {
            if (bps == 1) return 1;
            if (bps == 5) return 10;
            if (bps == 30) return 60;
            if (bps == 100) return 200;
            return int24(uint24(bps));
        }
    }

    /* msg.value rule (matches zRouter):
       tokenIn==ETH → exactIn: swapAmount, exactOut: amountLimit; else 0. */
    function _requiredMsgValue(
        bool exactOut,
        address tokenIn,
        uint256 swapAmount,
        uint256 amountLimit
    ) internal pure returns (uint256) {
        return tokenIn == address(0) ? (exactOut ? amountLimit : swapAmount) : 0;
    }

    function _bestSingleHop(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 amount,
        uint256 slippageBps,
        uint256 deadline
    )
        internal
        view
        returns (bool ok, Quote memory q, bytes memory data, uint256 amountLimit, uint256 msgValue)
    {
        try this.buildBestSwap(
            to, exactOut, tokenIn, tokenOut, amount, slippageBps, deadline
        ) returns (
            Quote memory q_, bytes memory d_, uint256 l_, uint256 v_
        ) {
            return (true, q_, d_, l_, v_);
        } catch {
            return (false, q, bytes(""), 0, 0);
        }
    }

    // ** MULTIHOP HELPER

    error ZeroAmount();

    function buildBestSwapViaETHMulticall(
        address to,
        address refundTo,
        bool exactOut, // false = exactIn, true = exactOut (on tokenOut)
        address tokenIn, // ERC20 or address(0) for ETH
        address tokenOut, // ERC20 or address(0) for ETH
        uint256 swapAmount, // exactIn: amount of tokenIn; exactOut: desired tokenOut
        uint256 slippageBps, // per-leg bound
        uint256 deadline
    )
        public
        view
        returns (
            Quote memory a,
            Quote memory b,
            bytes[] memory calls,
            bytes memory multicall,
            uint256 msgValue
        )
    {
        unchecked {
            require(swapAmount != 0, ZeroAmount());
            tokenIn = _normalizeETH(tokenIn);
            tokenOut = _normalizeETH(tokenOut);

            // Prevent stealable leftovers: if refundTo is the router itself, coerce to `to`.
            if (refundTo == ZROUTER && to != ZROUTER) refundTo = to;

            // ---------- FAST PATH #1: pure ETH<->WETH wrap/unwrap ----------
            bool trivialWrap = (tokenIn == address(0) && tokenOut == WETH)
                || (tokenIn == WETH && tokenOut == address(0));
            if (trivialWrap) {
                a = _asQuote(AMM.WETH_WRAP, swapAmount, swapAmount);
                b = Quote(AMM.UNI_V2, 0, 0, 0);

                if (tokenIn == address(0)) {
                    // ETH -> WETH: wrap exact amount then sweep WETH to recipient
                    calls = new bytes[](2);
                    calls[0] = abi.encodeWithSelector(IRouterExt.wrap.selector, swapAmount);
                    calls[1] = _sweepTo(WETH, to);
                    msgValue = swapAmount;
                } else {
                    // WETH -> ETH: deposit WETH, unwrap exact amount, sweep ETH to recipient
                    calls = new bytes[](3);
                    calls[0] = abi.encodeWithSelector(
                        IRouterExt.deposit.selector, WETH, uint256(0), swapAmount
                    );
                    calls[1] = abi.encodeWithSelector(IRouterExt.unwrap.selector, swapAmount);
                    calls[2] = _sweepTo(address(0), to);
                    msgValue = 0;
                }

                multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls);
                return (a, b, calls, multicall, msgValue);
            }

            // ---------- FAST PATH #2: direct ERC20↔ERC20 single-hop (may be Curve/V2/V3/V4/zAMM) ----------
            {
                (bool ok, Quote memory best, bytes memory callData,, uint256 val) = _bestSingleHop(
                    to, exactOut, tokenIn, tokenOut, swapAmount, slippageBps, deadline
                );
                if (ok) {
                    calls = new bytes[](1);
                    calls[0] = callData;

                    a = best;
                    b = Quote(AMM.UNI_V2, 0, 0, 0);
                    msgValue = val;

                    multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls);
                    return (a, b, calls, multicall, msgValue);
                }
            }

            // ---------- HUB LIST (majors) ----------
            address[6] memory HUBS = _hubs();

            // Track the best hub plan we can actually build
            HubPlan memory plan;
            plan.isExactOut = exactOut;

            for (uint256 h; h < HUBS.length; ++h) {
                address MID = HUBS[h];
                if (MID == tokenIn || MID == tokenOut) continue;

                if (!exactOut) {
                    // ---- overall exactIn: maximize final output ----
                    (bool okA, Quote memory qa, bytes memory ca,,) = _bestSingleHop(
                        ZROUTER, false, tokenIn, MID, swapAmount, slippageBps, deadline
                    );
                    // Skip Lido for intermediate hops: Lido functions don't depositFor,
                    // so the next leg can't find the tokens via transient storage.
                    if (!okA || qa.amountOut == 0 || qa.source == AMM.LIDO) continue;

                    uint256 midAmtForLeg2 = SlippageLib.limit(false, qa.amountOut, slippageBps);
                    (bool okB, Quote memory qb, bytes memory cb,,) = _bestSingleHop(
                        to, false, MID, tokenOut, midAmtForLeg2, slippageBps, deadline
                    );
                    if (!okB || qb.amountOut == 0) continue;

                    uint256 scoreOut = qb.amountOut; // maximize

                    if (!plan.found || scoreOut > plan.scoreOut) {
                        plan.found = true;
                        plan.mid = MID;
                        plan.isExactOut = false;
                        plan.a = qa;
                        plan.b = qb;
                        plan.ca = ca;
                        plan.cb = cb;
                        plan.scoreOut = scoreOut;
                    }
                } else {
                    // ---- overall exactOut: minimize total input ----
                    // Always route both legs through ZROUTER to avoid correctness issues
                    // with prefunding V2 pools (Curve/zAMM don't mark transient for the pair,
                    // and exactOut prefund risks donating excess to LPs).
                    (bool okB, Quote memory qb, bytes memory cb,,) = _bestSingleHop(
                        ZROUTER, true, MID, tokenOut, swapAmount, slippageBps, deadline
                    );
                    if (!okB || qb.amountIn == 0 || qb.source == AMM.LIDO) continue;

                    uint256 midRequired = qb.amountIn;
                    uint256 midLimit = SlippageLib.limit(true, midRequired, slippageBps);

                    (bool okA, Quote memory qa, bytes memory ca,,) =
                        _bestSingleHop(ZROUTER, true, tokenIn, MID, midLimit, slippageBps, deadline);
                    if (!okA || qa.amountIn == 0 || qa.source == AMM.LIDO) continue;

                    uint256 scoreIn = qa.amountIn; // minimize

                    if (!plan.found || scoreIn < plan.scoreIn) {
                        plan.found = true;
                        plan.mid = MID;
                        plan.isExactOut = true;
                        plan.a = qa;
                        plan.b = qb;
                        plan.ca = ca;
                        plan.cb = cb;
                        plan.scoreIn = scoreIn;
                    }
                }
            }

            if (!plan.found) revert NoRoute();

            // ---------- materialize the chosen plan into calls ----------
            if (!plan.isExactOut) {
                // exactIn path: two calls, no sweeps
                calls = new bytes[](2);
                calls[0] = plan.ca; // hop-1 tokenIn -> MID (exactIn)
                // hop-2: swapAmount=0 so router auto-consumes full MID balance
                calls[1] = _buildCalldataFromBest(
                    to,
                    false,
                    plan.mid,
                    tokenOut,
                    0,
                    SlippageLib.limit(false, plan.b.amountOut, slippageBps),
                    slippageBps,
                    deadline,
                    plan.b
                );
                a = plan.a;
                b = plan.b;
                // If tokenIn is ETH, hop-1 needs ETH for exactIn
                msgValue = (tokenIn == address(0)) ? swapAmount : 0;
            } else {
                // exactOut path: both legs route to ZROUTER, then explicit sweeps.
                // Unconditionally sweep all possible leftover tokens to avoid stranding
                // funds in the router (where sweep() is public).
                bool chaining = (to == ZROUTER);
                bool ethInput = (tokenIn == address(0));

                // Count finalization calls (when not chaining, sweep everything out):
                //   1) tokenOut delivery (exact swapAmount)
                //   2) MID leftover refund (over-production from slippage buffer)
                //   3) tokenIn leftover refund (any venue can leave dust in exactOut)
                //   4) ETH dust refund (when tokenIn is ETH)
                uint256 extra;
                if (!chaining) {
                    extra++; // tokenOut delivery
                    extra++; // MID leftover
                    if (!ethInput) extra++; // tokenIn leftover (ERC20)
                    extra++; // ETH dust (always: even non-ETH input can have ETH from unwraps)
                }

                calls = new bytes[](2 + extra);
                uint256 k;
                calls[k++] = plan.ca; // hop-1 tokenIn -> MID (exactOut, to=ZROUTER)
                calls[k++] = plan.cb; // hop-2 MID -> tokenOut (exactOut, to=ZROUTER)

                if (!chaining) {
                    // Deliver exact output amount to recipient
                    calls[k++] = abi.encodeWithSelector(
                        IRouterExt.sweep.selector, tokenOut, uint256(0), swapAmount, to
                    );
                    // Refund leftover MID (as-is, WETH stays as WETH)
                    calls[k++] = _sweepTo(plan.mid, refundTo);
                    // Refund leftover tokenIn (ERC20 only; ETH covered by ETH dust sweep)
                    if (!ethInput) {
                        calls[k++] = _sweepTo(tokenIn, refundTo);
                    }
                    // Refund any ETH dust
                    calls[k++] = _sweepTo(address(0), refundTo);
                }

                a = plan.a;
                b = plan.b;
                // If tokenIn is ETH, hop-1 exactOut needs ETH equal to its maxIn limit
                msgValue = ethInput ? SlippageLib.limit(true, plan.a.amountIn, slippageBps) : 0;
            }

            multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls);
            return (a, b, calls, multicall, msgValue);
        }
    }

    // ** 3-HOP MULTIHOP BUILDER

    /// @notice Encode a non-Curve single-hop swap from a Quote with an arbitrary
    ///         swapAmount.  Pass swapAmount = 0 so the router auto-reads its own
    ///         token balance as the input amount (exactIn only).
    function _buildSwapFromQuote(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline,
        Quote memory q
    ) internal pure returns (bytes memory) {
        if (q.source == AMM.UNI_V2 || q.source == AMM.SUSHI) {
            return abi.encodeWithSelector(
                IZRouter.swapV2.selector,
                to,
                exactOut,
                tokenIn,
                tokenOut,
                swapAmount,
                amountLimit,
                q.source == AMM.SUSHI ? type(uint256).max : deadline
            );
        } else if (q.source == AMM.ZAMM) {
            return abi.encodeWithSelector(
                IZRouter.swapVZ.selector,
                to,
                exactOut,
                q.feeBps,
                tokenIn,
                tokenOut,
                0,
                0,
                swapAmount,
                amountLimit,
                deadline
            );
        } else if (q.source == AMM.UNI_V3) {
            return abi.encodeWithSelector(
                IZRouter.swapV3.selector,
                to,
                exactOut,
                uint24(q.feeBps * 100),
                tokenIn,
                tokenOut,
                swapAmount,
                amountLimit,
                deadline
            );
        } else if (q.source == AMM.UNI_V4) {
            return abi.encodeWithSelector(
                IZRouter.swapV4.selector,
                to,
                exactOut,
                uint24(q.feeBps * 100),
                _spacingFromBps(uint16(q.feeBps)),
                tokenIn,
                tokenOut,
                swapAmount,
                amountLimit,
                deadline
            );
        }
        revert NoRoute();
    }

    /// @notice Build a 3-hop exactIn multicall:
    ///           tokenIn ─[Leg1]→ MID1 ─[Leg2]→ MID2 ─[Leg3]→ tokenOut
    ///
    ///         Legs 2 & 3 use swapAmount = 0 so the router auto-consumes the
    ///         previous leg's output via balanceOf().
    ///
    ///         Route discovery: tries every ordered pair (MID1, MID2) from the
    ///         hub list and picks the path that maximizes final output.
    ///         All AMMs (V2/Sushi/V3/V4/zAMM/Curve) are considered for each leg.
    function build3HopMulticall(
        address to,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 slippageBps,
        uint256 deadline
    )
        public
        view
        returns (
            Quote memory a,
            Quote memory b,
            Quote memory c,
            bytes[] memory calls,
            bytes memory multicall,
            uint256 msgValue
        )
    {
        unchecked {
            require(swapAmount != 0, ZeroAmount());
            tokenIn = _normalizeETH(tokenIn);
            tokenOut = _normalizeETH(tokenOut);
            address[6] memory HUBS = _hubs();

            Route3 memory r;

            for (uint256 i; i < HUBS.length; ++i) {
                address MID1 = HUBS[i];
                if (MID1 == tokenIn || MID1 == tokenOut) continue;

                Quote memory qa = _quoteBestSingleHop(false, tokenIn, MID1, swapAmount);
                if (qa.amountOut == 0 || qa.source == AMM.LIDO) continue;

                uint256 mid1Amt = SlippageLib.limit(false, qa.amountOut, slippageBps);

                for (uint256 j; j < HUBS.length; ++j) {
                    address MID2 = HUBS[j];
                    if (MID2 == tokenIn || MID2 == tokenOut || MID2 == MID1) continue;

                    Quote memory qb = _quoteBestSingleHop(false, MID1, MID2, mid1Amt);
                    if (qb.amountOut == 0) continue;

                    uint256 mid2Amt = SlippageLib.limit(false, qb.amountOut, slippageBps);

                    Quote memory qc = _quoteBestSingleHop(false, MID2, tokenOut, mid2Amt);
                    if (qc.amountOut == 0) continue;

                    if (!r.found || qc.amountOut > r.score) {
                        r.found = true;
                        r.a = qa;
                        r.b = qb;
                        r.c = qc;
                        r.mid1 = MID1;
                        r.mid2 = MID2;
                        r.score = qc.amountOut;
                    }
                }
            }

            if (!r.found) revert NoRoute();

            calls = new bytes[](3);

            // Leg 1: via buildBestSwap (handles all AMMs including Curve)
            (a, calls[0],, msgValue) =
                buildBestSwap(ZROUTER, false, tokenIn, r.mid1, swapAmount, slippageBps, deadline);

            // Legs 2 & 3: build calldata for any AMM type with swapAmount=0
            calls[1] = _buildCalldataFromBest(
                ZROUTER,
                false,
                r.mid1,
                r.mid2,
                0,
                SlippageLib.limit(false, r.b.amountOut, slippageBps),
                slippageBps,
                deadline,
                r.b
            );

            calls[2] = _buildCalldataFromBest(
                to,
                false,
                r.mid2,
                tokenOut,
                0,
                SlippageLib.limit(false, r.c.amountOut, slippageBps),
                slippageBps,
                deadline,
                r.c
            );

            b = r.b;
            c = r.c;
            multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls);
        }
    }

    /// @dev Build calldata for any AMM type including Curve, using a pre-computed quote.
    function _buildCalldataFromBest(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 slippageBps,
        uint256 deadline,
        Quote memory q
    ) internal view returns (bytes memory) {
        if (q.source == AMM.CURVE) {
            (,, address pool, bool useUnd, bool isStab, uint8 ci, uint8 cj) = quoteCurve(
                exactOut, tokenIn, tokenOut, swapAmount == 0 ? q.amountIn : swapAmount, 8
            );
            if (pool != address(0)) {
                (bytes memory cd,,) = _buildCurveSwapCalldata(
                    to,
                    exactOut,
                    tokenIn,
                    tokenOut,
                    swapAmount,
                    slippageBps,
                    deadline,
                    pool,
                    useUnd,
                    isStab,
                    ci,
                    cj,
                    q.amountIn,
                    q.amountOut
                );
                if (cd.length > 0) return cd;
            }
        }
        if (q.source == AMM.LIDO) {
            return _buildLidoSwap(to, exactOut, tokenOut, swapAmount);
        }
        // Default: V2/Sushi/V3/V4/ZAMM
        return
            _buildSwapFromQuote(
                to, exactOut, tokenIn, tokenOut, swapAmount, amountLimit, deadline, q
            );
    }

    // ====================== SPLIT ROUTING ======================

    /// @notice Build a split swap that divides the input across 2 venues for better execution.
    ///         ExactIn only. Tries splits [100/0, 75/25, 50/50, 25/75, 0/100] across the
    ///         top 2 venues and picks the best total output.
    function buildSplitSwap(
        address to,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 slippageBps,
        uint256 deadline
    ) public view returns (Quote[2] memory legs, bytes memory multicall, uint256 msgValue) {
        unchecked {
            require(swapAmount != 0, ZeroAmount());
            tokenIn = _normalizeETH(tokenIn);
            tokenOut = _normalizeETH(tokenOut);

            // Gather all individual venue quotes
            (, Quote[] memory baseQuotes) = getQuotes(false, tokenIn, tokenOut, swapAmount);

            // Collect candidates: base quotes + Curve
            // Filter out LIDO (uses callvalue(), unsafe in multicall splits) and WETH_WRAP.
            uint256 numCandidates;
            Quote[] memory candidates = new Quote[](baseQuotes.length + 1);
            for (uint256 i; i < baseQuotes.length; ++i) {
                if (baseQuotes[i].source == AMM.LIDO || baseQuotes[i].source == AMM.WETH_WRAP) {
                    continue;
                }
                candidates[numCandidates++] = baseQuotes[i];
            }

            // Add Curve candidate
            {
                (uint256 cin, uint256 cout, address pool,,,,) =
                    quoteCurve(false, tokenIn, tokenOut, swapAmount, 8);
                if (pool != address(0) && cout > 0) {
                    candidates[numCandidates] = _asQuote(AMM.CURVE, cin, cout);
                    numCandidates++;
                }
            }

            // Find top 2 by output
            uint256 best1Idx;
            uint256 best2Idx;
            uint256 best1Out;
            uint256 best2Out;

            for (uint256 i; i < numCandidates; ++i) {
                if (candidates[i].amountOut > best1Out) {
                    best2Out = best1Out;
                    best2Idx = best1Idx;
                    best1Out = candidates[i].amountOut;
                    best1Idx = i;
                } else if (candidates[i].amountOut > best2Out) {
                    best2Out = candidates[i].amountOut;
                    best2Idx = i;
                }
            }

            if (best1Out == 0) revert NoRoute();

            // If only 1 venue has output, just use 100/0
            if (best2Out == 0 || best1Idx == best2Idx) {
                legs[0] = candidates[best1Idx];
                // Build single swap
                (, bytes memory cd,, uint256 mv) =
                    buildBestSwap(to, false, tokenIn, tokenOut, swapAmount, slippageBps, deadline);
                bytes[] memory calls_ = new bytes[](1);
                calls_[0] = cd;
                multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls_);
                msgValue = mv;
                return (legs, multicall, msgValue);
            }

            Quote memory venue1 = candidates[best1Idx];
            Quote memory venue2 = candidates[best2Idx];

            // Try splits: [100/0, 75/25, 50/50, 25/75, 0/100]
            uint256[5] memory splitPcts = [uint256(100), 75, 50, 25, 0];
            uint256 bestTotalOut;
            uint256 bestSplit;

            for (uint256 s; s < 5; ++s) {
                uint256 amt1 = (swapAmount * splitPcts[s]) / 100;
                uint256 amt2 = swapAmount - amt1;

                uint256 out1;
                uint256 out2;

                if (amt1 > 0) {
                    Quote memory q1 = _requoteForSource(false, tokenIn, tokenOut, amt1, venue1);
                    out1 = q1.amountOut;
                }
                if (amt2 > 0) {
                    Quote memory q2 = _requoteForSource(false, tokenIn, tokenOut, amt2, venue2);
                    out2 = q2.amountOut;
                }

                uint256 total = out1 + out2;
                if (total > bestTotalOut) {
                    bestTotalOut = total;
                    bestSplit = s;
                }
            }

            // Build the winning split
            uint256 finalAmt1 = (swapAmount * splitPcts[bestSplit]) / 100;
            uint256 finalAmt2 = swapAmount - finalAmt1;

            if (finalAmt2 == 0 || finalAmt1 == 0) {
                // 100/0 or 0/100 split — single swap
                uint256 idx = finalAmt2 == 0 ? 0 : 1;
                Quote memory v = idx == 0 ? venue1 : venue2;
                legs[idx] = _requoteForSource(false, tokenIn, tokenOut, swapAmount, v);
                (, bytes memory cd,, uint256 mv) =
                    buildBestSwap(to, false, tokenIn, tokenOut, swapAmount, slippageBps, deadline);
                bytes[] memory calls_ = new bytes[](1);
                calls_[0] = cd;
                multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls_);
                msgValue = mv;
            } else {
                // True split: both legs pull from user
                legs[0] = _requoteForSource(false, tokenIn, tokenOut, finalAmt1, venue1);
                legs[1] = _requoteForSource(false, tokenIn, tokenOut, finalAmt2, venue2);

                uint256 limit1 = SlippageLib.limit(false, legs[0].amountOut, slippageBps);
                uint256 limit2 = SlippageLib.limit(false, legs[1].amountOut, slippageBps);

                // For ETH input, route to ZROUTER to prevent premature refund, then sweep.
                bool ethIn = tokenIn == address(0);
                address legTo = ethIn ? ZROUTER : to;

                // Curve legs with ETH input need a pre-wrap + route patch:
                // swapCurve checks msg.value == amountIn for ETH input, which fails
                // in multicall where msg.value is the total for all legs. We wrap
                // ETH→WETH into transient storage, then patch route[0] to WETH so
                // swapCurve treats it as WETH input and bypasses the msg.value check.
                bool wrapLeg1 = ethIn && legs[0].source == AMM.CURVE;
                bool wrapLeg2 = ethIn && legs[1].source == AMM.CURVE;
                // ETH input needs: sweep tokenOut + sweep ETH dust (2 extra calls)
                uint256 numCalls = 2 + (ethIn ? 2 : 0) + (wrapLeg1 ? 1 : 0) + (wrapLeg2 ? 1 : 0);
                bytes[] memory calls_ = new bytes[](numCalls);
                uint256 ci;

                if (wrapLeg1) {
                    calls_[ci++] = abi.encodeWithSelector(IRouterExt.wrap.selector, finalAmt1);
                }
                {
                    bytes memory cd1 = _buildCalldataFromBest(
                        legTo,
                        false,
                        tokenIn,
                        tokenOut,
                        finalAmt1,
                        limit1,
                        slippageBps,
                        deadline,
                        legs[0]
                    );
                    // Patch route[0] from ETH to WETH so swapCurve bypasses the
                    // msg.value==amountIn check and consumes pre-wrapped WETH instead.
                    if (wrapLeg1) assembly ("memory-safe") { mstore(add(cd1, 100), WETH) }
                    calls_[ci++] = cd1;
                }
                if (wrapLeg2) {
                    calls_[ci++] = abi.encodeWithSelector(IRouterExt.wrap.selector, finalAmt2);
                }
                {
                    bytes memory cd2 = _buildCalldataFromBest(
                        legTo,
                        false,
                        tokenIn,
                        tokenOut,
                        finalAmt2,
                        limit2,
                        slippageBps,
                        deadline,
                        legs[1]
                    );
                    if (wrapLeg2) assembly ("memory-safe") { mstore(add(cd2, 100), WETH) }
                    calls_[ci++] = cd2;
                }
                if (ethIn) {
                    // Sweep output token to recipient
                    calls_[ci++] = _sweepTo(tokenOut, to);
                    // Sweep any leftover ETH dust (V3/V4 refund excess msg.value to router
                    // during delegatecall; sweep prevents it from being stealable)
                    calls_[ci++] = _sweepTo(address(0), to);
                }

                multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls_);
                msgValue = ethIn ? swapAmount : 0;
            }
        }
    }

    // ====================== V4 HOOKED SPLIT ======================

    /// @dev Quote V4 hooked pool, returning 0 on failure.
    ///      quoteV4 simulates raw AMM math only — it does NOT simulate the hook's
    ///      afterSwap callback which can modify the swap delta (e.g. protocol fees).
    ///      We reduce the output by the hook's afterSwap fee so that slippage limits
    ///      and venue comparisons reflect the real post-fee amount.
    function _tryQuoteV4Hooked(
        address tokenIn,
        address tokenOut,
        uint256 amount,
        uint24 fee,
        int24 tick,
        address hook
    ) internal view returns (uint256 out) {
        try this.quoteV4(false, tokenIn, tokenOut, fee, tick, hook, amount) returns (
            uint256, uint256 o
        ) {
            out = o;
        } catch {
            return 0;
        }
        // PNKSTR hook: afterSwap takes 10% of output (feeBips=1000 stored in hook slot 0).
        if (hook == 0xfAaad5B731F52cDc9746F2414c823eca9B06E844) {
            out = (out * 9000) / 10000;
        }
    }

    /// @dev Build execute(V4_ROUTER) calldata for a V4 hooked pool swap (ETH input only).
    function _buildV4HookedCalldata(
        address to,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline,
        uint24 hookPoolFee,
        int24 hookTickSpacing,
        address hookAddress
    ) internal pure returns (bytes memory) {
        // Sort tokens for the V4 pool key (currency0 < currency1)
        (address c0, address c1) = tokenIn < tokenOut ? (tokenIn, tokenOut) : (tokenOut, tokenIn);
        bool zeroForOne = tokenIn == c0;
        bytes memory swapData = abi.encodeWithSelector(
            IV4Router.swapExactTokensForTokens.selector,
            swapAmount,
            amountLimit,
            zeroForOne,
            IV4PoolKey(c0, c1, hookPoolFee, hookTickSpacing, hookAddress),
            "",
            to,
            deadline
        );
        return abi.encodeWithSelector(IZRouter.execute.selector, V4_ROUTER, swapAmount, swapData);
    }

    /// @notice Build a split swap that includes a V4 hooked pool as a candidate.
    ///         ExactIn only. Gathers standard venues + Curve + the hooked pool,
    ///         finds the top 2, tries splits [100/0, 75/25, 50/50, 25/75, 0/100],
    ///         and returns the optimal multicall.
    function buildSplitSwapHooked(
        address to,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 slippageBps,
        uint256 deadline,
        uint24 hookPoolFee,
        int24 hookTickSpacing,
        address hookAddress
    ) public view returns (Quote[2] memory legs, bytes memory multicall, uint256 msgValue) {
        unchecked {
            require(swapAmount != 0, ZeroAmount());
            tokenIn = _normalizeETH(tokenIn);
            tokenOut = _normalizeETH(tokenOut);

            // ---- Gather candidates ----
            // Filter out LIDO (uses callvalue(), unsafe in multicall splits) and WETH_WRAP.
            (, Quote[] memory baseQuotes) = getQuotes(false, tokenIn, tokenOut, swapAmount);
            uint256 n;
            Quote[] memory cands = new Quote[](baseQuotes.length + 2);
            for (uint256 i; i < baseQuotes.length; ++i) {
                if (baseQuotes[i].source == AMM.LIDO || baseQuotes[i].source == AMM.WETH_WRAP) {
                    continue;
                }
                cands[n++] = baseQuotes[i];
            }

            // Curve
            {
                (uint256 ci_, uint256 co_, address p_,,,,) =
                    quoteCurve(false, tokenIn, tokenOut, swapAmount, 8);
                if (p_ != address(0) && co_ > 0) {
                    cands[n] = _asQuote(AMM.CURVE, ci_, co_);
                    n++;
                }
            }

            // V4 Hooked — ETH input only (ERC20 input hits Unauthorized on V4_ROUTER)
            uint256 hIdx = type(uint256).max;
            if (tokenIn == address(0)) {
                uint256 ho_ = _tryQuoteV4Hooked(
                    tokenIn, tokenOut, swapAmount, hookPoolFee, hookTickSpacing, hookAddress
                );
                if (ho_ > 0) {
                    hIdx = n;
                    cands[n] = Quote(AMM.V4_HOOKED, 0, swapAmount, ho_);
                    n++;
                }
            }

            // ---- Top 2 ----
            uint256 idx1;
            uint256 idx2;
            uint256 out1;
            uint256 out2;
            for (uint256 i; i < n; ++i) {
                if (cands[i].amountOut > out1) {
                    out2 = out1;
                    idx2 = idx1;
                    out1 = cands[i].amountOut;
                    idx1 = i;
                } else if (cands[i].amountOut > out2) {
                    out2 = cands[i].amountOut;
                    idx2 = i;
                }
            }
            if (out1 == 0) revert NoRoute();

            bool ethIn = tokenIn == address(0);

            // ---- Single venue fallback ----
            if (out2 == 0 || idx1 == idx2) {
                legs[0] = cands[idx1];
                if (idx1 == hIdx) {
                    uint256 lim = SlippageLib.limit(false, legs[0].amountOut, slippageBps);
                    bytes[] memory c_ = new bytes[](1);
                    c_[0] = _buildV4HookedCalldata(
                        to,
                        tokenIn,
                        tokenOut,
                        swapAmount,
                        lim,
                        deadline,
                        hookPoolFee,
                        hookTickSpacing,
                        hookAddress
                    );
                    multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, c_);
                    msgValue = ethIn ? swapAmount : 0;
                } else {
                    (, bytes memory cd,, uint256 mv) = buildBestSwap(
                        to, false, tokenIn, tokenOut, swapAmount, slippageBps, deadline
                    );
                    bytes[] memory c_ = new bytes[](1);
                    c_[0] = cd;
                    multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, c_);
                    msgValue = mv;
                }
                return (legs, multicall, msgValue);
            }

            // ---- Try splits ----
            bool v1h = (idx1 == hIdx);
            bool v2h = (idx2 == hIdx);
            Quote memory venue1 = cands[idx1];
            Quote memory venue2 = cands[idx2];

            uint256[5] memory pcts = [uint256(100), 75, 50, 25, 0];
            uint256 bestTotal;
            uint256 bestS;

            for (uint256 s; s < 5; ++s) {
                uint256 a1 = (swapAmount * pcts[s]) / 100;
                uint256 a2 = swapAmount - a1;
                uint256 o1_;
                uint256 o2_;

                if (a1 > 0) {
                    o1_ = v1h
                        ? _tryQuoteV4Hooked(
                            tokenIn, tokenOut, a1, hookPoolFee, hookTickSpacing, hookAddress
                        )
                        : _requoteForSource(false, tokenIn, tokenOut, a1, venue1).amountOut;
                }
                if (a2 > 0) {
                    o2_ = v2h
                        ? _tryQuoteV4Hooked(
                            tokenIn, tokenOut, a2, hookPoolFee, hookTickSpacing, hookAddress
                        )
                        : _requoteForSource(false, tokenIn, tokenOut, a2, venue2).amountOut;
                }

                uint256 t = o1_ + o2_;
                if (t > bestTotal) {
                    bestTotal = t;
                    bestS = s;
                }
            }

            // ---- Build winning split ----
            uint256 fa1 = (swapAmount * pcts[bestS]) / 100;
            uint256 fa2 = swapAmount - fa1;

            if (fa1 == 0 || fa2 == 0) {
                // 100/0 or 0/100 — single venue
                uint256 winner = fa1 == 0 ? 1 : 0;
                bool wh = winner == 0 ? v1h : v2h;
                if (wh) {
                    uint256 ho_ = _tryQuoteV4Hooked(
                        tokenIn, tokenOut, swapAmount, hookPoolFee, hookTickSpacing, hookAddress
                    );
                    legs[winner] = Quote(AMM.V4_HOOKED, 0, swapAmount, ho_);
                    uint256 lim = SlippageLib.limit(false, ho_, slippageBps);
                    bytes[] memory c_ = new bytes[](1);
                    c_[0] = _buildV4HookedCalldata(
                        to,
                        tokenIn,
                        tokenOut,
                        swapAmount,
                        lim,
                        deadline,
                        hookPoolFee,
                        hookTickSpacing,
                        hookAddress
                    );
                    multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, c_);
                    msgValue = ethIn ? swapAmount : 0;
                } else {
                    Quote memory v = winner == 0 ? venue1 : venue2;
                    legs[winner] = _requoteForSource(false, tokenIn, tokenOut, swapAmount, v);
                    (, bytes memory cd,, uint256 mv) = buildBestSwap(
                        to, false, tokenIn, tokenOut, swapAmount, slippageBps, deadline
                    );
                    bytes[] memory c_ = new bytes[](1);
                    c_[0] = cd;
                    multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, c_);
                    msgValue = mv;
                }
                return (legs, multicall, msgValue);
            }

            // ---- True split: build both legs ----
            if (v1h) {
                uint256 ho_ = _tryQuoteV4Hooked(
                    tokenIn, tokenOut, fa1, hookPoolFee, hookTickSpacing, hookAddress
                );
                legs[0] = Quote(AMM.V4_HOOKED, 0, fa1, ho_);
            } else {
                legs[0] = _requoteForSource(false, tokenIn, tokenOut, fa1, venue1);
            }
            if (v2h) {
                uint256 ho_ = _tryQuoteV4Hooked(
                    tokenIn, tokenOut, fa2, hookPoolFee, hookTickSpacing, hookAddress
                );
                legs[1] = Quote(AMM.V4_HOOKED, 0, fa2, ho_);
            } else {
                legs[1] = _requoteForSource(false, tokenIn, tokenOut, fa2, venue2);
            }

            uint256 lim1 = SlippageLib.limit(false, legs[0].amountOut, slippageBps);
            uint256 lim2 = SlippageLib.limit(false, legs[1].amountOut, slippageBps);

            address legTo = ethIn ? ZROUTER : to;

            // Curve legs with ETH input need a pre-wrap
            bool wrapLeg1 = ethIn && !v1h && legs[0].source == AMM.CURVE;
            bool wrapLeg2 = ethIn && !v2h && legs[1].source == AMM.CURVE;
            uint256 nc = 2 + (ethIn ? 2 : 0) + (wrapLeg1 ? 1 : 0) + (wrapLeg2 ? 1 : 0);
            bytes[] memory calls_ = new bytes[](nc);
            uint256 ci;

            // Leg 1
            if (wrapLeg1) {
                calls_[ci++] = abi.encodeWithSelector(IRouterExt.wrap.selector, fa1);
            }
            if (v1h) {
                calls_[ci++] = _buildV4HookedCalldata(
                    legTo,
                    tokenIn,
                    tokenOut,
                    fa1,
                    lim1,
                    deadline,
                    hookPoolFee,
                    hookTickSpacing,
                    hookAddress
                );
            } else {
                bytes memory cd1 = _buildCalldataFromBest(
                    legTo, false, tokenIn, tokenOut, fa1, lim1, slippageBps, deadline, legs[0]
                );
                if (wrapLeg1) assembly ("memory-safe") { mstore(add(cd1, 100), WETH) }
                calls_[ci++] = cd1;
            }

            // Leg 2
            if (wrapLeg2) {
                calls_[ci++] = abi.encodeWithSelector(IRouterExt.wrap.selector, fa2);
            }
            if (v2h) {
                calls_[ci++] = _buildV4HookedCalldata(
                    legTo,
                    tokenIn,
                    tokenOut,
                    fa2,
                    lim2,
                    deadline,
                    hookPoolFee,
                    hookTickSpacing,
                    hookAddress
                );
            } else {
                bytes memory cd2 = _buildCalldataFromBest(
                    legTo, false, tokenIn, tokenOut, fa2, lim2, slippageBps, deadline, legs[1]
                );
                if (wrapLeg2) assembly ("memory-safe") { mstore(add(cd2, 100), WETH) }
                calls_[ci++] = cd2;
            }

            // Final sweeps for ETH input
            if (ethIn) {
                calls_[ci++] = _sweepTo(tokenOut, to);
                // Sweep any leftover ETH dust (prevents stealable balance in router)
                calls_[ci++] = _sweepTo(address(0), to);
            }

            multicall = abi.encodeWithSelector(IRouterExt.multicall.selector, calls_);
            msgValue = ethIn ? swapAmount : 0;
        }
    }

    /// @dev Re-quote for a specific AMM source at a given amount.
    function _requoteForSource(
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 amount,
        Quote memory source
    ) internal view returns (Quote memory q) {
        AMM src = source.source;
        uint256 fee = source.feeBps;
        uint256 ai;
        uint256 ao;
        if (src == AMM.UNI_V2 || src == AMM.SUSHI) {
            (ai, ao) = quoteV2(exactOut, tokenIn, tokenOut, amount, src == AMM.SUSHI);
            fee = 30;
        } else if (src == AMM.UNI_V3) {
            (ai, ao) = quoteV3(exactOut, tokenIn, tokenOut, uint24(fee * 100), amount);
        } else if (src == AMM.UNI_V4) {
            (ai, ao) = quoteV4(
                exactOut,
                tokenIn,
                tokenOut,
                uint24(fee * 100),
                _spacingFromBps(uint16(fee)),
                address(0),
                amount
            );
        } else if (src == AMM.ZAMM) {
            (ai, ao) = quoteZAMM(exactOut, fee, tokenIn, tokenOut, 0, 0, amount);
        } else if (src == AMM.CURVE) {
            (uint256 cin, uint256 cout, address pool,,,,) =
                quoteCurve(exactOut, tokenIn, tokenOut, amount, 8);
            if (pool == address(0)) return q;
            return _asQuote(AMM.CURVE, cin, cout);
        } else {
            (q,) = getQuotes(exactOut, tokenIn, tokenOut, amount);
            return q;
        }
        return Quote(src, fee, ai, ao);
    }

    // ====================== SNWAP CALLDATA BUILDER ======================

    /// @notice Encode IZRouter.snwap calldata. Sets msgValue = amountIn if tokenIn == address(0).
    function buildSnwapCalldata(
        address tokenIn,
        uint256 amountIn,
        address recipient,
        address tokenOut,
        uint256 amountOutMin,
        address executor,
        bytes memory executorData
    ) public pure returns (bytes memory callData, uint256 msgValue) {
        callData = abi.encodeWithSelector(
            IZRouter.snwap.selector,
            tokenIn,
            amountIn,
            recipient,
            tokenOut,
            amountOutMin,
            executor,
            executorData
        );
        msgValue = (tokenIn == address(0)) ? amountIn : 0;
    }
}

function _sortTokens(address tokenA, address tokenB)
    pure
    returns (address token0, address token1, bool zeroForOne)
{
    (token0, token1) = (zeroForOne = tokenA < tokenB) ? (tokenA, tokenB) : (tokenB, tokenA);
}

address constant WETH = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
address constant USDC = 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48;
address constant USDT = 0xdAC17F958D2ee523a2206206994597C13D831ec7;
address constant DAI = 0x6B175474E89094C44Da98b954EedeAC495271d0F;
address constant WBTC = 0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599;
address constant WSTETH = 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0;
address constant STETH = 0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84;

interface IStETH {
    function getTotalShares() external view returns (uint256);
    function getTotalPooledEther() external view returns (uint256);
}

address constant ZROUTER = 0x000000000000FB114709235f1ccBFfb925F600e4;
address constant V4_ROUTER = 0x00000000000044a361Ae3cAc094c9D1b14Eece97;

struct IV4PoolKey {
    address currency0;
    address currency1;
    uint24 fee;
    int24 tickSpacing;
    address hooks;
}

interface IV4Router {
    function swapExactTokensForTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        bool zeroForOne,
        IV4PoolKey calldata poolKey,
        bytes calldata hookData,
        address to,
        uint256 deadline
    ) external payable returns (int256);
}

interface IRouterExt {
    function unwrap(uint256 amount) external payable;
    function wrap(uint256 amount) external payable;
    function deposit(address token, uint256 id, uint256 amount) external payable;
    function multicall(bytes[] calldata data) external payable returns (bytes[] memory);
    function sweep(address token, uint256 id, uint256 amount, address to) external payable;
}

address constant V2_FACTORY = 0x5C69bEe701ef814a2B6a3EDD4B1652CB9cc5aA6f;
bytes32 constant V2_POOL_INIT_CODE_HASH =
    0x96e8ac4277198ff8b6f785478aa9a39f403cb768dd02cbee326c3e7da348845f;

address constant SUSHI_FACTORY = 0xC0AEe478e3658e2610c5F7A4A2E1777cE9e4f2Ac;
bytes32 constant SUSHI_POOL_INIT_CODE_HASH =
    0xe18a34eb0e04b04f7a0ac29a6e80748dca96319b42c54d679cb821dca90c6303;

// ** CURVE

// ---- MetaRegistry (mainnet) ----
address constant CURVE_METAREGISTRY = 0xF98B45FA17DE75FB1aD0e7aFD971b0ca00e379fC;
address constant CURVE_ETH = 0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE;

// ---- Curve interfaces ----
interface ICurveMetaRegistry {
    function find_pools_for_coins(address from, address to) external view returns (address[] memory);
    function get_coin_indices(address pool, address from, address to, uint256 handler_id)
        external
        view
        returns (int128 i, int128 j, bool isUnderlying);
}

interface ICurveStableLike {
    function get_dy(int128 i, int128 j, uint256 dx) external view returns (uint256);
    function get_dx(int128 i, int128 j, uint256 dy) external view returns (uint256);
    // meta (underlying) variants
    function get_dy_underlying(int128 i, int128 j, uint256 dx) external view returns (uint256);
    function get_dx_underlying(int128 i, int128 j, uint256 dy) external view returns (uint256);
}

interface ICurveCryptoLike {
    function get_dy(uint256 i, uint256 j, uint256 dx) external view returns (uint256);
    function get_dx(uint256 i, uint256 j, uint256 dy) external view returns (uint256);
}

library SlippageLib {
    uint256 constant BPS = 10_000;

    error SlippageBpsTooHigh();

    function limit(bool exactOut, uint256 quoted, uint256 bps) internal pure returns (uint256) {
        require(bps < BPS, SlippageBpsTooHigh());
        unchecked {
            if (exactOut) {
                // maxIn = ceil(quotedIn * (1 + bps/BPS))
                return (quoted * (BPS + bps) + BPS - 1) / BPS;
            } else {
                // minOut = floor(quotedOut * (1 - bps/BPS))
                return (quoted * (BPS - bps)) / BPS;
            }
        }
    }
}

interface IZRouter {
    function swapV2(
        address to,
        bool exactOut,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline
    ) external payable returns (uint256 amountIn, uint256 amountOut);

    function swapVZ(
        address to,
        bool exactOut,
        uint256 feeOrHook,
        address tokenIn,
        address tokenOut,
        uint256 idIn,
        uint256 idOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline
    ) external payable returns (uint256 amountIn, uint256 amountOut);

    function swapV3(
        address to,
        bool exactOut,
        uint24 swapFee,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline
    ) external payable returns (uint256 amountIn, uint256 amountOut);

    function swapV4(
        address to,
        bool exactOut,
        uint24 swapFee,
        int24 tickSpace,
        address tokenIn,
        address tokenOut,
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline
    ) external payable returns (uint256 amountIn, uint256 amountOut);

    function swapCurve(
        address to,
        bool exactOut,
        address[11] calldata route,
        uint256[4][5] calldata swapParams, // [i, j, swap_type, pool_type]
        address[5] calldata basePools, // for meta pools (only used by type=2 get_dx)
        uint256 swapAmount,
        uint256 amountLimit,
        uint256 deadline
    ) external payable returns (uint256 amountIn, uint256 amountOut);

    function snwap(
        address tokenIn,
        uint256 amountIn,
        address recipient,
        address tokenOut,
        uint256 amountOutMin,
        address executor,
        bytes calldata executorData
    ) external payable returns (uint256 amountOut);

    function snwapMulti(
        address tokenIn,
        uint256 amountIn,
        address recipient,
        address[] calldata tokensOut,
        uint256[] calldata amountsOutMin,
        address executor,
        bytes calldata executorData
    ) external payable returns (uint256[] memory amountsOut);

    function exactETHToSTETH(address to) external payable returns (uint256 shares);
    function exactETHToWSTETH(address to) external payable returns (uint256 wstOut);
    function ethToExactSTETH(address to, uint256 exactOut) external payable;
    function ethToExactWSTETH(address to, uint256 exactOut) external payable;

    function revealName(string calldata label, bytes32 secret, address to)
        external
        payable
        returns (uint256 tokenId);

    function execute(address target, uint256 value, bytes calldata data)
        external
        payable
        returns (bytes memory result);

    function addLiquidity(
        ZAMMPoolKey calldata poolKey,
        uint256 amount0Desired,
        uint256 amount1Desired,
        uint256 amount0Min,
        uint256 amount1Min,
        address to,
        uint256 deadline
    ) external payable returns (uint256 amount0, uint256 amount1, uint256 liquidity);

    function permit(address token, uint256 value, uint256 deadline, uint8 v, bytes32 r, bytes32 s)
        external
        payable;
    function permitDAI(uint256 nonce, uint256 expiry, uint8 v, bytes32 r, bytes32 s)
        external
        payable;
    function permit2TransferFrom(
        address token,
        uint256 amount,
        uint256 nonce,
        uint256 deadline,
        bytes calldata signature
    ) external payable;
}

struct ZAMMPoolKey {
    uint256 id0;
    uint256 id1;
    address token0;
    address token1;
    uint256 feeOrHook;
}

